module: dylan
rcs-header: $Header: /scm/cvs/src/mindy/libraries/dylan/list.dylan,v 1.2 2000/01/24 04:58:25 andreas Exp $

//======================================================================
//
// Copyright (c) 1994  Carnegie Mellon University
// Copyright (c) 1998, 1999, 2000  Gwydion Dylan Maintainers
// All rights reserved.
// 
// Use and copying of this software and preparation of derivative
// works based on this software are permitted, including commercial
// use, provided that the following conditions are observed:
// 
// 1. This copyright notice must be retained in full on any copies
//    and on appropriate parts of any derivative works.
// 2. Documentation (paper or online) accompanying any system that
//    incorporates this software, or any part of it, must acknowledge
//    the contribution of the Gwydion Project at Carnegie Mellon
//    University, and the Gwydion Dylan Maintainers.
// 
// This software is made available "as is".  Neither the authors nor
// Carnegie Mellon University make any warranty about the software,
// its performance, or its conformity to any specification.
// 
// Bug reports should be sent to <gd-bugs@gwydiondylan.org>; questions,
// comments and suggestions are welcome at <gd-hackers@gwydiondylan.org>.
// Also, see http://www.gwydiondylan.org/ for updates and documentation. 
//
//======================================================================
//
//  This file contains the support for lists that isn't built in.
//



//// Construction.

def make(cls == <list>, #rest keys, #key size = 0, fill = #f) 
 => l :: <list>;
  let result = for (i from 0 below size,
		    list = #() then pair(fill, list))
	       finally
		 list;
	       end;
  apply(initialize, result, keys);
  result;
end;


// Note: list(...) is built into Mindy.



//// Iteration protocol.

def list_fip_next_state(list :: <list>, state :: <list>) => next :: <list>;
    tail(state);
end;

def list_fip_finished-state?(list :: <list>, state :: <list>, limit)
    state == #();
end;

def list_fip_current_key(list :: <list>, state :: <list>) => cur-key :: <integer>;
    for (key from 0,
	 scan = list then tail(scan),
	 until: scan == state)
      if (scan == #())
	error("State not part of list?");
      end;
    finally
      key;
    end;
end;


def list_fip_current_element(list :: <list>, state :: <list>) => cur-elt :: <object>;
    head(state);
end;

def list_fip_current_element-setter(value :: <object>, list :: <list>, state :: <list>)
   => value :: <object>;
    head(state) := value;
end;

def list_fip_copy_state(list :: <list>, state :: <list>) => state :: <list>;
    state;
end;

def forward-iteration-protocol (list :: <list>)
 => (initial-state :: <object>,
     limit :: <object>,
     next-state :: <function>,
     finished-state? :: <function>,
     current-key :: <function>,
     current-element :: <function>,
     current-element-setter :: <function>,
     copy-state :: <function>);
  values(list, #f, list_fip_next_state, list_fip_finished-state?,
	 list_fip_current_key, list_fip_current_element,
	 list_fip_current_element-setter, list_fip_copy_state);
end;


//// Collection routines.

// Note: size(<list>) is built into Mindy.

def empty?(list :: <empty-list>) => answer :: <boolean>;
  #t;
end;

def empty?(list :: <pair>) => answer :: <boolean>;
  #f;
end;

def type-for-copy(list :: <list>) => type :: <class>;
  <list>;
end;

def member? (value, l :: <list>, #key test: test = \==)
 => answer :: <boolean>;
  let done        = #f;
  let lapped-slow = #f;                // Has fast lapped slow?

  block (return)
    for (slow = l        then tail (slow),
	 fast = tail (l) then if (lapped-slow) fast;
			      else tail (tail (fast))
			      end,
	 until: done | slow == #() )

      if (test (value, head (slow)))
	return(#t);
      elseif (fast == slow)
	done   := lapped-slow;    // Since fast goes twice the speed,
	                          // need to give slow a chance to
	                          // catch up.
	lapped-slow := #t;
      end;
    end;

    #f;     // If we've gotten this far, the for loop didn't find the element
  end;
end;

def map (proc :: <function>, 
	 collection :: <empty-list>, 
	 #rest more) => l :: <list>;
  #();
end;

def map-as (a_class :: singleton (<list>), proc :: <function>,
            l :: <list>, #next next-method, #rest more-lists)
 => result :: <list>;
  if (every? (rcurry ( instance?, <list> ), more-lists))
    for (l          = l          then tail (l),
	 more-lists = more-lists then map (tail, more-lists),
	 result     = #()        then pair (apply (proc, head (l),
						   map (head, more-lists)),
					    result),
	 until: ( l == #() ) | any? (rcurry (\==, #()), more-lists))
    finally
      reverse! (result);
    end;

  else
    next-method ();
  end;
end;

def any?(proc :: <function>, l :: <empty-list>, #rest more)
 => answer :: <boolean>;
  #f;
end;

def every? (proc :: <function>, l :: <empty-list>, #rest more)
 => answer :: <boolean>;
  #t;
end;


//// Sequence routines.

def add(l :: <list>, new) => new-list :: <list>;
  pair(new, l);
end;

def add!(l :: <list>, new) => new-list :: <list>;
  pair (new, l);
end;

def remove(l :: <list>, value, #key test: test = \==,
           count: count) => new-l :: <list>;
  let result    = #();
  let remaining = l;

  until ( remaining == #() )
    if ( (count ~= 0) & test (head (remaining), value) )
      remaining := tail (remaining);
      count     := count & (count - 1);         // False if undefined,
			                        // count - 1 otherwise
    else
      result    := pair (head (remaining), result);
      remaining := tail (remaining);
    end;
  end;

  reverse! (result);
end;
      
def remove! (l :: <list>, value, #key test: test = \==,
             count: count) => new-l :: <list>;
  let result    = l;
  let prev      = #f;
  let remaining = l;

  until ( remaining == #() )
    if (count = 0 | ~ (test (head (remaining), value)))
      prev      := remaining;
      remaining := tail (remaining);
    elseif (prev)
      tail (prev) := tail (remaining);
      remaining   := tail (remaining);
      count       := count & (count - 1);
    else
      result      := tail (remaining);
      prev        := #f;
      remaining   := tail (remaining);
      count       := count & (count - 1);
    end;
  end;

  result;
end;

// If there are duplicates, this returns the LAST identical element,
// and not the first like the example on page 107 would indicate.

def remove-duplicates (l :: <list>, #key test: test = \== )
 => new-l :: <list>;
  let result    = #();
  let prev      = #f;
  let remaining = l;

  until ( remaining == #() )
    if (member? (head (remaining), tail (remaining), test: test))
      remaining   := tail (remaining);
    elseif (prev)
      let next = list (head (remaining));
      tail (prev) := next;
      prev        := next;
      remaining   := tail (remaining);
    else
      let new = list (head (remaining));
      result      := new;
      prev        := new;
      remaining   := tail (remaining);
    end;
  end;

  result;
end;

def remove-duplicates! ( l :: <list>, #key test: test = \== )
 => new-l :: <list>;
  let result    = l;
  let prev      = #f;
  let remaining = l;
  
  until ( remaining == #() )
    if ( ~ member? (head (remaining), tail (remaining), test: test))
      prev        := remaining;
      remaining   := tail (remaining);
    elseif (prev)
      tail (prev) := tail (remaining);
      remaining   := tail (remaining);
    else
      result      := tail (remaining);
      prev        := #f;
      remaining   := tail (remaining);
    end;
  end;

  result;
end;

def replace-subsequence! (l :: <list>, seq :: <sequence>,
		          #key start: start = 0, end: stop)
 => new-l :: <list>;
  let result = pair (#f, l);
  let prev   = result;

  for (i from 1 to start)
    prev := tail (prev);
  end;

  if (~ stop)
    stop := start + size (seq);
  end;

  let after-hole = for (after-hole = tail (prev) then tail (after-hole),
			index = start then index + 1,
			until: index = stop)
		   finally after-hole;
		   end;

  for (elt in seq)
    let next = pair (elt, #f);
    tail (prev) := next;
    prev        := next;
  end;
  
  tail (prev) := after-hole;
  tail (result);
end;

def reverse  (l :: <list>) => l :: <list>;
  let result = #();
  let remaining = l;

  until ( remaining == #() )
    result := pair (head (remaining), result);
    remaining := tail (remaining);
  end;

  result;
end;

def reverse! (l :: <list>) => l :: <list>;
  let result    = #();
  let remaining = l;

  until ( remaining == #() )
    let t = tail (remaining);
    tail (remaining) := result;
    result           := remaining;
    remaining        := t;
  end;

  result;
end;


//// =

// We have to define a method on <list>/<list>, because = is defined to
// work on dotted lists, and the sequence version of = will try calling
// every?, which will flame out on dotted lists.

// Will be called when you compare an <empty-list> to a <pair>
// or vice versa.
def \= (a :: <list>, b :: <list>) => answer :: <boolean>;
  #f;
end;


def \= (a :: <empty-list>, b :: <empty-list>) => answer :: <boolean>;
  #t;
end;


def \= (a :: <pair>, b :: <pair>) => answer :: <boolean>;
  ( head (a) = head (b) )  &  ( tail (a) = tail (b) );
end;
