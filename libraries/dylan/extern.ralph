module: extern
rcs-header: $Header: /scm/cvs/src/mindy/libraries/dylan/extern.dylan,v 1.2 2000/01/24 04:58:25 andreas Exp $

//======================================================================
//
// Copyright (c) 1994  Carnegie Mellon University
// Copyright (c) 1998, 1999, 2000  Gwydion Dylan Maintainers
// All rights reserved.
// 
// Use and copying of this software and preparation of derivative
// works based on this software are permitted, including commercial
// use, provided that the following conditions are observed:
// 
// 1. This copyright notice must be retained in full on any copies
//    and on appropriate parts of any derivative works.
// 2. Documentation (paper or online) accompanying any system that
//    incorporates this software, or any part of it, must acknowledge
//    the contribution of the Gwydion Project at Carnegie Mellon
//    University, and the Gwydion Dylan Maintainers.
// 
// This software is made available "as is".  Neither the authors nor
// Carnegie Mellon University make any warranty about the software,
// its performance, or its conformity to any specification.
// 
// Bug reports should be sent to <gd-bugs@gwydiondylan.org>; questions,
// comments and suggestions are welcome at <gd-hackers@gwydiondylan.org>.
// Also, see http://www.gwydiondylan.org/ for updates and documentation. 
//
//======================================================================
//
// This file contains definitions useful for calling native C functions from
// within Mindy.  Many of these are intended to support the code produced by
// Melange rather than being explicitly referenced by users.
//

def gcf-unbound = pair(#f, #f); // hack

// This is potentially useful, but will probably be overshadowed by Melange.
// It combines the functionality of "find-c-function" and
// "constrain-c-function" to get usable function in one step.
//
def get-c-function
    (name :: <string>,
     #key args, rest = ~args, result = <object>, file = gcf-unbound)
 => (result :: <c-function>);
  let real-args = if (args) as(<list>, args) else #() end;
  let real-result = if (instance?(result, <sequence>)) as(<list>, result)
		    else list(result)
		    end;
  let fun = if (file == gcf-unbound)
	      find-c-function(name)
	    else
	      find-c-function(name, file: file);
	    end;
  fun & constrain-c-function(fun, real-args, rest, real-result);
end;

// These will be used by "make" and "destroy".
//
def malloc = get-c-function("malloc", args: list(<integer>),
		            result: <statically-typed-pointer>);
def calloc = get-c-function("calloc",
			    args: list(<integer>, <integer>),
			    result: <statically-typed-pointer>);
def c-free = get-c-function("free",
			    args: list(<statically-typed-pointer>),
			    result: #());

// Uses "free" to deallocate a native C pointer.
//
generic destroy (ptr :: <statically-typed-pointer>) => ();

// Generic "dereference" operation.  Methods will be defined for individual
// pointer types.
//
generic pointer-value(ptr :: <statically-typed-pointer>, #key index);
generic pointer-value-setter(value :: <object>, ptr :: <statically-typed-pointer>, #key index);

// Generic function which will be extended with methods that describe the
// sizes of particular pointer types.  If the size is unknown, it will return
// 0.  "Structure-size" is defined as an alias for compatibility with Creole.
//
generic content-size(cls :: limited(<class>, subclass-of: <statically-typed-pointer>))
 => (result :: <integer>);

def structure-size = content-size;

// The import-value and export-value functions can be extended to add
// user-defined type mapping to Melange.  Default methods are provided for
// <statically-typed-pointer> and for <boolean>.
//
// Note that these functions are not compatible with the identically named
// functions in Creole.
//
generic export-value(low-level-class :: <class>, high-level-value :: <object>)
 => (low-level-value :: <object>);

generic import-value(high-level-class :: <class>, low-level-value :: <object>)
 => (high-level-value :: <object>);

def export-value
    (low-level-class :: <class>, high-level-value :: <object>)
 => (low-level-value :: <object>);
  as(low-level-class, high-level-value);
end;

def import-value
    (high-level-class :: <class>, low-level-value :: <object>)
 => (high-level-value :: <object>);
  as(high-level-class, low-level-value);
end;

// Default make method for pointer values.  You may create a vector by
// specifying "element-count:" and may adjust the size explicitly by
// specifying "extra-bytes:".
//
def make(cls :: limited(<class>, subclass-of: <statically-typed-pointer>),
         #rest rest,
         #key extra-bytes :: <integer> = 0,
              pointer,
              element-count :: <integer> = 1,
         #all-keys)
 => (result :: <statically-typed-pointer>);
  if (pointer)
    select (pointer by instance?)
      <statically-typed-pointer>,
      <integer> =>
	as(cls, pointer);
      otherwise =>
	error("Invalid pointer: keyword in make: %=", pointer);
    end;
  else
    if (element-count < 1)
      error("Bad element-count: in make: %=", element-count);
    end;

    let ptr = as(cls,
		 calloc((content-size(cls) + extra-bytes), element-count));
    if (ptr = null-pointer) error("Make failed to allocate memory.") end;

    apply(initialize, ptr, rest);

    ptr;
  end;
end;

// Explicitly destroys C pointers, since we cannot garbage collect them.
//
def destroy (ptr :: <statically-typed-pointer>) => ();
  c-free(ptr);
end;

class <machine-pointer> (<statically-typed-pointer>) end;

// <C-string> corresponds to C's native "char *" type.  We provide basic
// functions to that it obeys the protocol of <string>.
//
class <c-string>
    (<stretchy-collection>, <string>, <statically-typed-pointer>) 
end;

// We come up with an ambiguity in this special case, so define a method which
// resolves it.
// 
def as(cls == <c-string>, value :: <c-string>) => (result :: <c-string>);
  value;
end;

// We might as well use the native c library routines when they do the right
// thing anyway.
//
def strcmp
  = get-c-function("strcmp", args: list(<c-string>, <c-string>),
		   result: <integer>);
def strlen
  = get-c-function("strlen", args: list(<c-string>), result: <integer>);

def type-for-copy (string :: <c-string>) => type :: <type>;
  <byte-string>;
end;

def make(cls :: limited(<class>, subclass-of: <c-string>),
         #next next, #key size: sz = 0, fill = ' ')
 => result :: <c-string>;
  let result = next(cls, element-count: sz + 1);
  let fill-byte = as(<integer>, fill);
  for (i from 0 below sz)
    unsigned-byte-at(result, offset: i) := fill-byte;
  end;
  unsigned-byte-at(result, offset: sz) := 0;
  result;
end;

def forward-iteration-protocol (str :: <c-string>)
 => (initial-state :: <object>,
     limit :: <object>,
     next-state :: <function>,
     finished-state? :: <function>,
     current-key :: <function>,
     current-element :: <function>,
     current-element-setter :: <function>,
     copy-state :: <function>);
  values(0, #f,
	 method (str, state) state + 1 end,
	 method (str, state, limit)
	   str = null-pointer | unsigned-byte-at(str, offset: state) == 0;
	 end,
	 method (str, state) state end,
	 method (str, state)
	   as(<character>, unsigned-byte-at(str, offset: state));
	 end,
	 method (value :: <character>, str, state)
	   unsigned-byte-at(str, offset: state) := as(<integer>, value);
	 end,
	 method (str, state) state end);
end;

def pointer-value (ptr :: <c-string>, #key index = 0)
  as(<character>, unsigned-byte-at(ptr, offset: index));
end;

def pointer-value-setter
    (char :: <character>, ptr :: <c-string>, #key index = 0);
  unsigned-byte-at(ptr, offset: index) := as(<integer>, char);
end;

def content-size
    (value :: limited(<class>, subclass-of: <c-string>))
 => (result :: <integer>);
  1;
end;

def size (string :: <c-string>)
 => result :: <integer>;
  case
    (string = null-pointer) => 0;
    otherwise => strlen(string);
  end;
end;

def empty? (string :: <c-string>) => (result :: <boolean>);
  string = null-pointer | unsigned-byte-at(string) = 0;
end;

def space-byte = as(<integer>, ' ');
def size-setter (value :: <integer>, string :: <c-string>)
 => value :: <integer>;
  let sz = string.size;
  case
    value = null-pointer =>
      error("Cannot set size of null <c-string>s");
    value == sz =>
      #f;
    value > sz =>
      for (i from sz below value)
	unsigned-byte-at(string, offset: i) := space-byte;
      end;
      unsigned-byte-at(string, offset: value) := 0;
    value < 0 =>
      error("Cannot set size below zero.");
    value < sz =>
      unsigned-byte-at(string, offset: value) := 0;
  end;
end;

// These methods actually attempt to avoid writing past the end of a string.
// This makes them somewhat slow.  If faster (unsafe) access is required, you
// should use pointer-value instead.
//
def element(vec :: <c-string>, index :: <integer>, #key default = $not-supplied)
 => (result :: <character>);
  let sz = vec.size;
  case
    index >= 0 & index < sz =>
      pointer-value(vec, index: index);
    default == $not-supplied =>
      error("No such element in %=: %=", vec, index);
    otherwise =>
      default;
  end;
end;
    
def element-setter(char :: <byte-character>, vec :: <c-string>, index :: <integer>)
 => char :: <byte-character>;
  let sz = vec.size;
  if (index < 0) error("Negative keys not allowed in strings.") end;
  if (index >= sz) vec.size := index + 1 end;
  pointer-value(vec, index: index) := char;
end;

def \<(str1 :: <c-string>, str2 :: <c-string>)
 => result :: <boolean>;
  case
    (str1.empty?) => ~str2.empty?;
    (str2.empty?) => #f;
    otherwise => strcmp(str1, str2) < 0;
  end;
end;

def \=(str1 :: <c-string>, str2 :: <c-string>)
 => result :: <boolean>;
  let empty1 = str1.empty?;
  let empty2 = str2.empty?;
  case
    empty1 & empty2 => #t;
    empty1 | empty2 => #f;
    otherwise => strcmp(str1, str2) == 0;
  end;
end;

// This is a very common operation, so let's make it fast.
//
def as (cls == <c-string>, str :: <byte-string>)
 => string :: <c-string>;
  let sz = str.size;
  let result = as(<c-string>, malloc(sz + 1));
  for (i from 0 below sz)
    unsigned-byte-at(result, offset: i) := as(<integer>, str[i]);
  end;
  unsigned-byte-at(result, offset: sz) := 0;
  result;
end;

// This is a very common operation, so let's make it fast.
//
def as (cls == <byte-string>, str :: <c-string>)
 => string :: <byte-string>;
  let sz = str.size;
  let result = make(<string>, size: sz);
  for (i from 0 below sz)
    result[i] := as(<character>, unsigned-byte-at(str, offset: i));
  end;
  result;
end;

//------------------------------------------------------------------------

def export-value (cls == <integer>, value :: <boolean>)
 => (result :: <integer>);
  if (value) 1 else 0 end;
end;

def import-value (cls == <boolean>, value :: <integer>)
 => (result :: <boolean>);
  value ~= 0;
end;

//------------------------------------------------------------------------

// <c-vector> can correspond to either true C vectors or to C pointers which
// are treated as vectors.  In the latter case, we won't know the size, so we
// will define it as "#f" (just like infinite lists have) and make sure that
// the iteration protocol can handle this.
//
// C vector types are automatically made members of <c-vector> by Melange.
// Pointer types will have to explicitly declare membership via a
// "superclasses:" option.
//
class <c-vector> (<vector>, <statically-typed-pointer>) end;

// C does not do bounds checking on vectors, and many of the interface files
// make use of this weakngess.  Therefore, we do no bounds checking either.
// Caveat emptor.
//
def element(vec :: <c-vector>, index :: <integer>, #key default)
 => (result :: <object>);
  pointer-value(vec, index: index);
end;

// C does not do bounds checking on vectors, and many of the interface files
// make use of this weakngess.  Therefore, we do no bounds checking either.
// Caveat emptor.
//
def element-setter(value :: <object>, vec :: <c-vector>, index :: <integer>)
 => value :: <object>;
  pointer-value(vec, index: index) := value;
end;

// For "normal" ponters, the size is "#f", indicating that the size is unknown
// and potentially infinite.  However, subtypes can redefine this to higher
// values and the iteration protocol will still work.
//
def size (vec :: <c-vector>) => (result :: <false>);
  #f;
end;

// Straightforward vector FIP.  We duplicate it here to avoid problems with
// possible recursive definitions of element.
//
def forward-iteration-protocol (vec :: <c-vector>)
 => (initial-state :: <object>,
     limit :: <object>,
     next-state :: <function>,
     finished-state? :: <function>,
     current-key :: <function>,
     current-element :: <function>,
     current-element-setter :: <function>,
     copy-state :: <function>);
  values(0, vec.size,
	 method (c, s) s + 1 end,	// next-state
	 method (c, s, l) s == l end, // finished-state?
	 method (c, s) s end,	// current-key
	 method (c, s) pointer-value(c, index: s) end, // current-element
	 method (v, c, s) pointer-value(c, index: s) := v end, // ""-setter
	 method (c, s) s end); // copy-state
end;
